// -*-C++-*-

/* Purpose: NCO/ncap2 script to calculate hyperpectral indices from the hyperspectral reflectances 
   ( this is the input variable rfl_img(wavelenth,y,x) ) 
   by default the averages of the required reflectances is calculated. (these have the postfix suffix "_avg" )
   These averages are then used to calculate the average of the indices. 

   If requried the average  indices can be calculated after the ratio step. 
   ( these  indices have the postfix suffix "_rba" )
   Note the following indices are NOT ratio's ( "WI"  )

    
   set the following flags on the ncap2 command line 
   flg_dbg - if set then print debug info (default 0) 
  
   flg_rba - if set then calculate the averages after the ratio step (default 1)
  
   flg_mss - set the _FillValue 1.0e36 to all values in selected reflectances where value <=0.0f (default 1)

   usage examples 
   ncap2 -v -O  -S hyperspectral_indices_make.nco   in.nc out.nc                                         ( use default flags)     
   ncap2 -v -O -s 'flg_dbg=1s;flg_rba=0s;flg_mss=1s;' -S hyperspectral_indices_make.nco   in.nc out.nc   ( set flags )


*/
   


if(!exists(flg_dbg))
   *flg_dbg=0s;  // [flg] Print debug info

if(!exists(flg_rba))
   *flg_rba=1s;  // [flg] calculate ratios before average

if(!exists(flg_mss)){
  *flg_mss=1s;  // [flg] add and set _FillValue to reflectance indices
  flt_mss=1.0e36f;
}




//required reflectances
@rfl_lst={"R445"s,"R450"s,"R470"s, "R500"s,"R512"s,"R531"s, "R540"s,"R550"s,"R570"s,"R586"s,"R590"s,"R600"s,"R650"s,"R670"s, "R680"s,"R690"s,"R700"s,"R705"s,"R710"s,"R720"s,"R740"s,"R750"s,"R760"s, "R780"s,"R790"s,"R800"s,"R900"s,"R970"s};  

// nb ONE-TO_ONE  correspondence with above list
@dbl_val_lst={ 4.45e-7d, 4.5e-7d, 4.7e-7d, 5.0e-7d, 5.12e-7, 5.31e-7, 5.4e-7, 5.5e-7, 5.7e-7,5.86e-7, 5.9e-7,6.0e-7,6.5e-7,6.7e-7, 6.8e-7, 6.9e-7, 7.0e-7, 7.05e-7, 7.1e-7,7.2e-7, 7.4e-7,7.5e-7,7.6e-7, 7.8e-7, 7.9e-7, 8.0e-7, 9.0e-7,9.7e-7};    

// use vpointers to calculate averages AFTER pixel ratios
@ind_lst={"NDVI"s,"SR"s, "OSAVI"s,"CHL"s,"msR705"s,"TCARI"s,"CarChap"s,"Car1Black"s,"Car2Black"s,"PRI570"s,"SIPI"s,"antGamon"s,"antGitelson"s,"CHLDela"s,"CI"s,"PRI586"s,"PRI512"s,"FRI1"s,"FRI2"s,"NDVI1"s,"RERI"s,"ZM"s,"REP"s,"NDRE"s,"TVI"s};



/* sanity check */
if( @rfl_lst.size() != @dbl_val_lst.size() )
{
  print("size difference between list @rfl_lst and @dbl_val_lst\n");
  exit(1);
}


/* extract the required reflectances - 
   nb min_coords() -extracts the index of the grid value nearest the required value 
   This step creates the vars iR445, iR450, iR470..iR970 */
{
  *idx=0;
  *sz=@rfl_lst.size();

  for(idx=0;idx<sz;idx++)
  {
    @R_nm=sprint(@rfl_lst(idx));
    @iR_nm=push("i",@R_nm);

    *@iR_nm=min_coords(wavelength,@dbl_val_lst(idx));
          
  }


}



/* sanity checks for required relectances -  make sure the are within resonable tolerance (1.0e-9) */
if( fabs(wavelength(iR445)-4.45e-7) > 0.01e-7d )  
{
  print("wavelength R445 not in wavelength coord\n");  
  exit(1);
}
if( fabs(wavelength(iR970)-9.70e-7) > 0.01e-7d )
{
  print("wavelength R970 not in wavelength coord\n");  
  exit(1);
}
if( fabs(wavelength(iR700)-7e-7) > 0.01e-7d )
{
  print("wavelength R700 not in wavelength coord\n");  
  exit(1);
}



/* This block hyperslabs the reflectances and their averages - with or with _FillValue */
/* That is the vars R445, R450..R970  and  R445_avg, R450_avg..R970_avg */
{
 

  if(flg_dbg)
    print(@rfl_lst);

  sz=@rfl_lst.size();

  for(idx=0; idx<sz ; idx++)
  {
    @var_nm=sprint(@rfl_lst(idx));
    @var_inm=push("i",@var_nm);   
    @var_nm_avg=push(@var_nm,"_avg");

    /* create R* variables from hyperslab */
    *@var_nm=rfl_img(*@var_inm,:,:);
   

    
    /* set and add missing value */   
   if(flg_mss) 
   {
    where (*@var_nm <= 0.0f)
      *@var_nm=flt_mss;  

    *@var_nm.set_miss(flt_mss);  
   }

    /* create avg of hyperslabs */
    *@var_nm_avg=*@var_nm.avg();
  
    if(flg_dbg)
       print(@var_nm,"%s\n");
 
  } 

}


NDVI = ( R900 -R680) / (R900+R680 );
NDVI@long_name="Normalized Difference Vegetation Index";
NDVI_avg = ( R900_avg -R680_avg) / (R900_avg+R680_avg );
NDVI_avg@long_name=NDVI@long_name;


SR=R900 / R680;
SR@long_name="Simple ratio";
SR_avg=R900_avg / R680_avg;
SR_avg@long_name=SR@long_name;



OSAVI=1.16f*( (R800-R670) ) /  ( R800+R670+0.16f);
OSAVI@long_name="Optimized Soil-Adjusted Vegetation index";
OSAVI_avg=1.16f*( (R800_avg-R670_avg) ) /  ( R800_avg+R670_avg+0.16f);
OSAVI_avg@long_name=OSAVI@long_name;


WI=R900-R970;
WI@long_name="water index";
WI_avg=R900_avg-R970_avg;
WI_avg@long_name=WI@long_name;


CHL=R750 / R550;
CHL@long_name="Chlorophyll index";
CHL_avg=R750_avg / R550_avg;
CHL_avg@long_name=CHL@long_name;

msR705= (R750-R445) / (R705-R445);
msR705@long_name="Modified simple ratio 705";
msR705_avg= (R750_avg-R445_avg) / (R705_avg-R445_avg);
msR705_avg@long_name=msR705@long_name;


TCARI=3.0f*(  (R700-R670)-0.2f * (R700-R550) * (R700/R670)  );
TCARI@long_name="Transformed chlorophyll absorption in reflectance index";
TCARI_avg=3.0f*(  (R700_avg-R670_avg)-0.2f * (R700_avg-R550_avg) * (R700_avg/R670_avg)  );
TCARI_avg@long_name=TCARI@long_name;



CarChap=R760/R500;
CarChap@long_name="Carotenoid index (Chappelle)";
CarChap_avg=R760_avg/R500_avg;
CarChap_avg@long_name=CarChap@long_name;


Car1Black=R800/R470;
Car1Black@long_name="Carotenoid index (BlackBurn)";
Car1Black_avg=R800_avg/R470_avg;
Car1Black_avg@long_name=Car1Black@long_name;



Car2Black= ( R800 - R470 ) / (R800 + R470);
Car2Black@long_name="Carotenoid index 2 (BlackBurn)";
Car2Black_avg= ( R800_avg - R470_avg ) / (R800_avg + R470_avg);
Car2Black_avg@long_name=Car2Black@long_name;



PRI570 = (R531 - R570) / (R531+R570);
PRI570@long_name="Photochemical reflectance index (570)";
PRI570_avg = (R531_avg - R570_avg) / (R531_avg+R570_avg);
PRI570_avg@long_name=PRI570@long_name;


SIPI =( R800 - R450) / (R800 + R650);
SIPI@long_name="Structure intensive pigment index";
SIPI_avg =( R800_avg - R450_avg) / (R800_avg + R650_avg);
SIPI_avg@long_name=SIPI@long_name;



antGamon=R650/R550;
antGamon@long_name="Anthocyanin (Gamon)";
antGamon_avg=R650_avg/R550_avg;
antGamon_avg@long_name=antGamon@long_name;



antGitelson=( 1.0f/R550 - 1.0f/R700)*R780;
antGitelson@long_name="Anthocyanin (Gitelson)";
antGitelson_avg=( 1.0f/R550_avg - 1.0f/R700_avg)*R780_avg;
antGitelson_avg@long_name=antGitelson@long_name;


CHLDela=( R540-R590 ) / ( R540 + R590);
CHLDela@long_name="Chlorophyll content";
CHLDela_avg=( R540_avg-R590_avg ) / ( R540_avg + R590_avg);
CHLDela_avg@long_name=CHLDela@long_name;



CI=( R750-R705 ) / ( R750 + R705);
CI@long_name="Chlorophyll index";
CI_avg=( R750_avg-R705_avg ) / ( R750_avg + R705_avg);
CI_avg@long_name=CI@long_name;



PRI586=( R531-R586) / ( R531 + R586);
PRI586@long_name="Photochemical reflectance index (586)";
PRI586_avg=( R531_avg-R586_avg) / ( R531_avg + R586_avg);
PRI586_avg@long_name=PRI586@long_name;



PRI512=( R531-R512) / ( R531 + R512);
PRI512@long_name="Photochemical reflectance index (512)";
PRI512_avg=( R531_avg-R512_avg) / ( R531_avg + R512_avg);
PRI512_avg@long_name=PRI512@long_name;


FRI1=R690 / R600;
FRI1@long_name="Fluorescence ratio index1";
FRI1_avg=R690_avg / R600_avg;
FRI1_avg@long_name=FRI1@long_name;



FRI2=R740 / R800;
FRI2@long_name="Fluorescence ratio iindices 2";
FRI2_avg=R740_avg / R800_avg;
FRI2_avg@long_name=FRI2@long_name;


NDVI1=(R800-R670)/ (R800+R670);
NDVI1@long_name="Normalized Difference Vegetation Index1";
NDVI1_avg=(R800_avg-R670_avg)/ (R800_avg+R670_avg);
NDVI1_avg@long_name=NDVI1@long_name;


RDVI=NDVI1*0.5f;
RDVI@long_name="Renormalized Difference Vegetation Index";
RDVI_avg=NDVI1_avg*0.5f;
RDVI_avg@long_name=RDVI@long_name;


RERI=R700/R670;
RERI@long_name="Red edge ratio index";
RERI_avg=R700_avg/R670_avg;
RERI_avg@long_name=RERI@long_name;




ZM= R750 / R710;
ZM@long_name="Red edge";
ZM_avg= R750_avg / R710_avg;
ZM@long_name=ZM@long_name;


REP=700.0f +40.0f*(((R670+R780)/2-R700) /(R740-R700));
REP@long_name="Red edge position";
REP_avg=700.0f +40.0f*(((R670_avg+R780_avg)/2-R700_avg) /(R740_avg-R700_avg));
REP_avg@long_name=REP@long_name;


NDRE=(R790-R720)/(R790+R720);
NDRE@long_name="Normalized difference vegetation index";
NDRE_avg=(R790_avg-R720_avg)/(R790_avg+R720_avg);
NDRE_avg@long_name=NDRE@long_name;

TVI=pow( 0.5f, (120.0f*(R750-R550)-200.0f*(R670-R550)));
TVI@long_name="Triangular Vegetation Index";
TVI_avg=pow( 0.5f, (120.0f*(R750_avg-R550_avg)-200.0f*(R670_avg-R550_avg)));
TVI_avg@long_name=TVI@long_name;



/* if required then calculate _rba - averages after ratios */
/* that is the vars NDVI_rba, SR_rba, OSAVI_rba..TVI_rba */
if(flg_rba)
{ 
  /* nb sz and idx are already RAM vars */

  if(flg_dbg)
    print(@ind_lst);
  
  sz=@ind_lst.size();

 
  for(idx=0;idx<sz;idx++)   
  {
     @rname=sprint(@ind_lst(idx));

     if(exists(*@rname))
     {
        /* ratio before average */
        @rname_rba=push(@rname,"_rba"); 
        *@rname_rba=*@rname.avg();
     }  
  }  
}  
